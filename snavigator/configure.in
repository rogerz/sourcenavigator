dnl Process this file with autoconf to produce a configure script.

dnl Initialization.
AC_INIT(hyper/sninit.c)
AC_PREREQ(2.12.2)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(sn,5.2.0)
AM_MAINTAINER_MODE

AC_CONFIG_AUX_DIR(..)

# The combination of Cygwin, autoconf 2.13. and cl from VC++
# needs extra hand holding.
case "${host}" in
*windows32*)
        CC="cl"
        CPP="cl -nologo -E"
        CFLAGS="-Z7 -Od -MD -nologo"
        ac_cv_exeext=".exe"
        ;;
esac

AC_CANONICAL_HOST

dnl Constants.  These _must_ come after the AC_INIT macro invocation.

dnl Put default arguments for kind of releases here. Override them later with
dnl individual --enable or --disable flags.

BUG_ADDRESS=support@cygnus.com
SUITE_NAME="Red Hat Embedded DevKit"
PRODUCT_NAME="Source-Navigator"
SDK_DIR=sdk
SENDMAIL_PARSER=sendmail
JAVA_PARSER=
JAVAEXE=jbrowser
SPLASHFILE=splashsn.gif
X11ICON=paf.xbm
SN_DOCSET=enterprise
SN_BOOKS="userguide progref"
SN_LAUNCH_SHELL=snavigator
AC_DEFINE(SN_ELIX, 0, [Define this if we are building with elix features.])
C_DEMO=c++_demo
ASM_DEMO=asm_demo
COB_DEMO=cob_demo
FOR_DEMO=for_demo
MONOP_DEMO=monop
JAV_DEMO=jav_demo
GETTING_STARTED_DIR=
GETTING_STARTED_INDEX=
GETTING_STARTED_NAME=
USING_GNUPRO_DIR=
USING_GNUPRO_INDEX=
USING_GNUPRO_NAME=
PROGRAMMERS_REFERENCE_DIR=progref
PROGRAMMERS_REFERENCE_INDEX=index_pr.html
PROGRAMMERS_REFERENCE_NAME="Programmer's Reference Guide"
ASSEMBLER_DIRS=ppc601-eabi

dnl Set all of the product specific variables.

dnl EL/IX specific features
AC_ARG_ENABLE(elix, [  --enable-elix          Enable EL/IX specific features.],enable_elix=yes,)
AM_CONDITIONAL(SNEDK, test x$enable_elix = xyes)

if test x$enable_elix = xyes; then
   AC_DEFINE(SN_ELIX, 1, [Define this if we are building with elix features.])
   SN_LAUNCH_SHELL=snedk
   ASM_DEMO=
   COB_DEMO=
   JAV_DEMO=
   FOR_DEMO=
   MONOP_DEMO=
   SN_DOCSET=edk
   SN_BOOKS="getstart userguide gnupro"
   GETTING_STARTED_DIR=getstart
   GETTING_STARTED_INDEX=index_gs.html
   GETTING_STARTED_NAME="Getting Started Guide"
   USING_GNUPRO_DIR=gnupro
   USING_GNUPRO_INDEX=index_gp.html
   USING_GNUPRO_NAME="Using GNUPro Toolkit"
   PROGRAMMERS_REFERENCE_DIR=
   PROGRAMMERS_REFERENCE_INDEX=
   PROGRAMMERS_REFERENCE_NAME=
fi

dnl Set the assembler parser
AC_ARG_ENABLE(assembly-parser, [  --enable-assembly-parser  Change the default assembly parser], ASSEMBLER_DIRS=$enableval,)

echo $ASSEMBLER_DIRS

dnl We can't use AC_CONFIG_SUBDIRS(parsers/assembly/$ASSEMBLER_DIRS)
dnl because AC_CONFIG_SUBDIRS in 2.5X does not allow a variable argument.

case "$ASSEMBLER_DIRS" in
    ppc601-eabi)
        AC_CONFIG_SUBDIRS(parsers/assembly/ppc601-eabi)
        ;;
    *)
        AC_MSG_ERROR([Unknow assembly parser $ASSEMBLER_DIRS.])
        ;;
esac

dnl The address to send bug reports to.
AC_ARG_ENABLE(bug-address, [  --enable-bug-address    Specify a non-default address to send bug reports to],
BUG_ADDRESS=$enableval,)

dnl Set the product name
AC_ARG_ENABLE(product-name, [  --enable-product-name    Specify the product name.],
PRODUCT_NAME=$enableval,)
AC_DEFINE_UNQUOTED(PRODUCT_NAME, "$PRODUCT_NAME", [Name of the product])

dnl Set the Suite name for this package
AC_ARG_ENABLE(suite-name, [  --enable-suite-name   Specify the suite name of the package],
SUITE_NAME=$enableval,)
AC_DEFINE_UNQUOTED(SUITE_NAME, "$SUITE_NAME", [Suite name of the product])

AC_SUBST(BUG_ADDRESS)
AC_SUBST(SUITE_NAME)
AC_SUBST(PRODUCT_NAME)
AC_SUBST(SDK_DIR)
AC_SUBST(SENDMAIL_PARSER)
AC_SUBST(JAVA_PARSER)
AC_SUBST(JAVAEXE)
AC_SUBST(SPLASHFILE)
AC_SUBST(X11ICON)
AC_SUBST(SN_DOCSET)
AC_SUBST(SN_LAUNCH_SHELL)
AC_SUBST(C_DEMO)
AC_SUBST(ASM_DEMO)
AC_SUBST(COB_DEMO)
AC_SUBST(FOR_DEMO)
AC_SUBST(MONOP_DEMO)
AC_SUBST(JAV_DEMO)
AC_SUBST(GETTING_STARTED_DIR)
AC_SUBST(GETTING_STARTED_INDEX)
AC_SUBST(GETTING_STARTED_NAME)
AC_SUBST(USING_GNUPRO_DIR)
AC_SUBST(USING_GNUPRO_INDEX)
AC_SUBST(USING_GNUPRO_NAME)
AC_SUBST(PROGRAMMERS_REFERENCE_DIR)
AC_SUBST(PROGRAMMERS_REFERENCE_INDEX)
AC_SUBST(PROGRAMMERS_REFERENCE_NAME)
AC_SUBST(SN_BOOKS)
AC_SUBST(ASSEMBLER_DIRS)

# Check for compiler

AC_PROG_CC
AC_PROG_CPP

AC_CACHE_CHECK([for cygwin32], ide_cv_os_cygwin32,
[AC_EGREP_CPP(lose, [
#ifdef __CYGWIN32__
lose
#endif],[ide_cv_os_cygwin32=yes],[ide_cv_os_cygwin32=no])])

AC_CACHE_CHECK([for Microsoft Visual C++], sn_cv_os_msvc,
[AC_EGREP_CPP(coool, [
#ifdef _MSC_VER
coool
#endif],[sn_cv_os_msvc=yes],[sn_cv_os_msvc=no])])

AC_CACHE_CHECK([for Mingw32], sn_cv_os_mingw32,
[AC_EGREP_CPP(coool, [
#ifdef __MINGW32__
coool
#endif],[sn_cv_os_mingw32=yes],[sn_cv_os_mingw32=no])])

if test x$ide_cv_os_cygwin32 = xyes ||
       test x$sn_cv_os_mingw32 = xyes ; then
   HYPER_CFLAGS="-Wall -fwritable-strings"
fi
AC_SUBST(HYPER_CFLAGS) 

dnl Checks for programs.

AC_EXEEXT

AC_OBJEXT
AC_PROG_AWK

# A developer might want to regenerate files with flex and
# bison, the flexgen and bisongen targets are used to do this.
# We don't want to include these targets in a normal build
# since we need to be able to build on systems that do not
# have flex and bison installed.

LEX="flex"
LEXLIB="-lfl"
YACC="bison -y"
AC_SUBST(LEX)
AC_SUBST(LEXLIB)
AC_SUBST(YACC)

AC_CHECK_TOOL(RANLIB, ranlib)
AC_CHECK_TOOL(WINDRES, windres)

dnl Put a plausible default for CC_FOR_BUILD in Makefile.  In the
dnl normal case, this will just set CC_FOR_BUILD to be CC.  People who
dnl are building with a cross compiler must know what they are doing.
if test -z "$CC_FOR_BUILD"; then
  if test "$cross_compiling" = yes; then
    CC_FOR_BUILD=gcc
  else
    CC_FOR_BUILD='$(CC)'
  fi
fi
AC_SUBST(CC_FOR_BUILD)

AM_CONDITIONAL(CROSS_COMPILING, test x$cross_compiling = xyes)

dnl If we are using GNU tar, we want to create old-style archives,
dnl because GNU tar by default creates archives that only it can read.
dnl On other tars, -o means something else, so we can't unconditionally
dnl use it.  GNU tar's --help output implies that it generates ANSI
dnl tar files, but it it wrong.
AC_PATH_PROGS(TAR, [gnutar gtar tar])
OLDTARFLAG=
if ($TAR --version) < /dev/null > /dev/null 2>&1; then
   OLDTARFLAG=o
fi
AC_SUBST(OLDTARFLAG)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([fcntl.h limits.h malloc.h netinet/in.h search.h strings.h sys/in.h sys/ioctl.h sys/time.h sys/times.h sys/utsname.h sys/param.h termio.h unistd.h stropt.h])

#--------------------------------------------------------------------
#	Supply a substitute for stdlib.h if it doesn't define strtol,
#	strtoul, or strtod (which it doesn't in some versions of SunOS).
#--------------------------------------------------------------------

AC_MSG_CHECKING(stdlib.h)
AC_HEADER_EGREP(strtol, stdlib.h, tk_ok=yes, tk_ok=no)
AC_HEADER_EGREP(strtoul, stdlib.h, , tk_ok=no)
AC_HEADER_EGREP(strtod, stdlib.h, , tk_ok=no)
if test $tk_ok = no; then
    AC_DEFINE(NO_STDLIB_H,,[Set this if Tk's stdlib.h should be used.])
fi
AC_MSG_RESULT($tk_ok)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

dnl The macro invocation below is currently commented out in order
dnl to support configuring with autoconf 2.13. Once the move to
dnl 2.5X is made, the manual check below can be replaced with
dnl the call to AC_CHECK_TYPES.

dnl AC_CHECK_TYPES([ssize_t, size_t, mode_t, off_t, pid_t])

AC_MSG_CHECKING(for ssize_t)
AC_CACHE_VAL(db_cv_type_ssize_t, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>], [ssize_t c = '0';],
db_cv_type_ssize_t=yes,
db_cv_type_ssize_t=no)])
AC_MSG_RESULT($db_cv_type_ssize_t)
if test "$db_cv_type_ssize_t" = yes; then
   AC_DEFINE(HAVE_SSIZE_T,,[Define if ssize_t is defined.])
fi

AC_MSG_CHECKING(for size_t)
AC_CACHE_VAL(db_cv_type_size_t, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>], [size_t c = '0';],
db_cv_type_size_t=yes,
db_cv_type_size_t=no)])
AC_MSG_RESULT($db_cv_type_size_t)
if test "$db_cv_type_size_t" = yes; then
   AC_DEFINE(HAVE_SIZE_T,,[Define if size_t is defined.])
fi

AC_MSG_CHECKING(for mode_t)
AC_CACHE_VAL(db_cv_type_mode_t, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>], [mode_t c = '0';],
db_cv_type_mode_t=yes,
db_cv_type_mode_t=no)])
AC_MSG_RESULT($db_cv_type_mode_t)
if test "$db_cv_type_mode_t" = yes; then
   AC_DEFINE(HAVE_MODE_T,,[Define if mode_t is defined.])
fi

AC_MSG_CHECKING(for off_t)
AC_CACHE_VAL(db_cv_type_off_t, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>], [off_t c = '0';],
db_cv_type_off_t=yes,
db_cv_type_off_t=no)])
AC_MSG_RESULT($db_cv_type_off_t)
if test "$db_cv_type_off_t" = yes; then
   AC_DEFINE(HAVE_OFF_T,,[Define if off_t is defined.])
fi

AC_MSG_CHECKING(for pid_t)
AC_CACHE_VAL(db_cv_type_pid_t, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>], [pid_t c = '0';],
db_cv_type_pid_t=yes,
db_cv_type_pid_t=no)])
AC_MSG_RESULT($db_cv_type_pid_t)
if test "$db_cv_type_pid_t" = yes; then
   AC_DEFINE(HAVE_PID_T,,[Define if pid_t is defined.])
fi

AC_MSG_CHECKING(for u_char)
AC_CACHE_VAL(db_cv_type_u_char, [dnl
AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>
#if defined(__MINGW32__)
# include <winsock.h>
#endif
], [u_char c = '0';], db_cv_type_u_char=yes,
db_cv_type_u_char=no)])
AC_MSG_RESULT($db_cv_type_u_char)
if test "$db_cv_type_u_char" = yes; then
   AC_DEFINE(HAVE_UCHAR_T,,[Define if uchar_t (and friends) are defined.])
fi

dnl Test for GNU libc.
AC_CACHE_CHECK([for GNU libc],sn_cv_lib_glibc,[AC_EGREP_CPP(yes, [
#include <features.h>
#ifdef __ELF__
# ifdef __GLIBC__
#  if __GLIBC__ >= 2
    yes
#  endif
# endif
#endif], sn_cv_lib_glibc=yes, sn_cv_lib_glibc=no)])
if test x$sn_cv_lib_glibc = xyes; then
   AC_DEFINE(HAVE_GLIBC,,[Define this if we link against GNU libc.])
fi

dnl Tcl ensures that strncasecmp is provided everywhere.  But in some
dnl situations we might need to declare it.  We check for that case
dnl here.
AC_EGREP_CPP(strncasecmp, [#include <string.h>],
  AC_DEFINE(HAVE_STRNCASECMP_DECL,,[Define this if <string.h> declares strncasecmp().]))

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MEMCMP
if test "$cross_compiling" = yes; then
  AC_CACHE_CHECK(whether setpgrp takes no argument, ac_cv_func_setpgrp_void,
  AC_TRY_COMPILE([#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif],
  [getpgrp (0); setpgrp (0, 0);],
  ac_cv_func_setpgrp_void=no, ac_cv_func_setpgrp_void=yes))
  if test $ac_cv_func_setpgrp_void = yes; then
    AC_DEFINE(SETPGRP_VOID,,[Define this if <unistd.h> declares setpgrp with no arguments.])
  fi
else
  AC_FUNC_SETPGRP
fi
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
AC_CHECK_FUNCS([getcwd getwd getopt grantpt mkdir ptsname putenv realpath regcomp rmdir select strcspn strdup strspn strstr strtod strtol strtoul uname unlockpt])

AM_CONDITIONAL(HAVE_GETOPT, test x$ac_cv_func_getopt = xyes)
AM_CONDITIONAL(HAVE_REALPATH, test x$ac_cv_func_realpath = xyes)

dnl If we have random, assumes we have srandom.  If we have drand48,
dnl assume we have srand48.  If we have rand, assume we have srand.
AC_CHECK_FUNCS(random drand48 rand, break)

# In the Windows environment, we need some additional flags and
# libraries.

if test x$ide_cv_os_cygwin32 = xyes; then
  WINFLAGS="-D_WINDOWS=1 -DWIN32"
  WIN32LIBS="-lcomdlg32 -ladvapi32 -lshell32 -luser32 -lgdi32"
  WIN32LDAPP="-Wl,--subsystem,windows"
  SNCONFIGDIR="win"
  ac_win_build="yes"
elif test x$sn_cv_os_msvc = xyes; then
  WINFLAGS="-D_WINDOWS=1 -DWIN32 -D__MSVC__ -DWINNT"
  WIN32LIBS="comdlg32.lib advapi32.lib shell32.lib user32.lib gdi32.lib"
  WIN32LDAPP="-subsystem:windows -entry:WinMainCRTStartup"
  SNCONFIGDIR="win"
  ac_win_build="yes"
elif test x$sn_cv_os_mingw32 = xyes; then
  WINFLAGS="-D_WINDOWS=1 -DWIN32 -DWINNT"
  WIN32LIBS="-lcomdlg32 -ladvapi32 -lshell32 -luser32 -lgdi32"
  WIN32LDAPP="-Wl,--subsystem,windows"
  SNCONFIGDIR="win"
  ac_win_build="yes"
else
  WINFLAGS=
  WIN32LIBS=
  WIN32LDAPP=
  SNCONFIGDIR="unix"
  ac_win_build="no"
fi

AC_SUBST(WINFLAGS)
AC_SUBST(WIN32LIBS)
AC_SUBST(WIN32LDAPP)
AM_CONDITIONAL(WINDOWS, test x$ac_win_build = xyes)
AM_CONDITIONAL(MSVC, test x$sn_cv_os_msvc = xyes)

dnl Check for -lsocket and -lnsl.  This is copied from Tcl.
AC_CACHE_CHECK([for socket libraries], tcl_cv_lib_sockets,
 [tcl_cv_lib_sockets=
  tcl_checkBoth=0
  unset ac_cv_func_connect
  AC_CHECK_FUNC(connect, tcl_checkSocket=0, tcl_checkSocket=1)
  if test "$tcl_checkSocket" = 1; then
      unset ac_cv_func_connect
      AC_CHECK_LIB(socket, main, tcl_cv_lib_sockets="-lsocket",
		   tcl_checkBoth=1)
  fi
  if test "$tcl_checkBoth" = 1; then
      tcl_oldLibs=$LIBS
      LIBS="$LIBS -lsocket -lnsl"
      unset ac_cv_func_accept
      AC_CHECK_FUNC(accept,
		    [tcl_checkNsl=0
		     tcl_cv_lib_sockets="-lsocket -lnsl"])
      unset ac_cv_func_accept
      LIBS=$tcl_oldLibs
  fi
  unset ac_cv_func_gethostbyname
  tcl_oldLibs=$LIBS
  LIBS="$LIBS $tcl_cv_lib_sockets"
  AC_CHECK_FUNC(gethostbyname, ,
		[AC_CHECK_LIB(nsl, main,
			      [tcl_cv_lib_sockets="$tcl_cv_lib_sockets -lnsl"])])
  unset ac_cv_func_gethostbyname
  LIBS=$tcl_oldLibs
])
SOCKLIB="$tcl_cv_lib_sockets"
AC_SUBST(SOCKLIB)

hold_libs="$LIBS"
LIBS="$LIBS $SOCKLIB"
AC_CHECK_FUNCS(gethostent gethostname)
LIBS="$hold_libs"

dnl Random other stuff

dnl Why do this?  Because it gets the definition into every
dnl Makefile.in without any extra work.  FIXME these should be in
dnl CY_AC_BERKELEY_DB.
DB_LIBDIR='$(top_builddir)/../db/PORT'
AC_SUBST(DB_LIBDIR)
DB_INCDIR='$(top_srcdir)/../db/include'
AC_SUBST(DB_INCDIR)
CY_AC_BERKELEY_DB

CYG_AC_PATH_TCLCONFIG
CYG_AC_LOAD_TCLCONFIG
CYG_AC_PATH_TKCONFIG
CYG_AC_LOAD_TKCONFIG
CYG_AC_PATH_TCLH
CYG_AC_PATH_TKH
CY_AC_PATH_TIXCONFIG
CY_AC_LOAD_TIXCONFIG
CYG_AC_PATH_ITCLCONFIG
CYG_AC_LOAD_ITCLCONFIG
CYG_AC_PATH_ITKCONFIG
CYG_AC_LOAD_ITKCONFIG

# Find the init.tcl file.

AC_MSG_CHECKING(for init.tcl)
AC_CACHE_VAL(ac_cv_c_tcl_libdir,[
dnl currently, only check the source tree
if test -f $srcdir/../tcl/library/init.tcl ; then
  ac_cv_c_tcl_libdir=`cd $srcdir/../tcl/library; pwd`
elif test -f $srcdir/../tcl/library/init.tcl; then
  ac_cv_c_tcl_libdir=`cd $srcdir/../tcl/library; pwd`
fi
])
AC_MSG_RESULT(${ac_cv_c_tcl_libdir})

# FIXME: Should use macro from config/ subdir!
AC_CHECK_PROG(CYGPATH, cygpath, cygpath -w, echo)

if test "$CYGPATH" != "echo" ; then
  tmp2="`$CYGPATH $ac_cv_c_tcl_libdir`"
  TCL_LIBRARY="`echo $tmp2 | sed -e s#\\\\\\\\#/#g`"
else
  TCL_LIBRARY=$ac_cv_c_tcl_libdir
fi
AC_SUBST(TCL_LIBRARY)


if test x$ac_win_build = xyes ; then
  HYPER_MAIN='winMain.$(OBJEXT)'
else
  HYPER_MAIN=
fi

AC_SUBST(HYPER_MAIN)

tmp="`cd $srcdir/gui; pwd`"
if test "$CYGPATH" != "echo" ; then
  tmp2="`$CYGPATH $tmp`"
  SN_GUI_DIR="`echo $tmp2 | sed -e s#\\\\\\\\#/#g`"
else
  SN_GUI_DIR=$tmp
fi
AC_SUBST(SN_GUI_DIR)

if test x${exec_prefix} = xNONE; then
  SN_SDK_EXEC_PREFIX='${prefix}/share/sdk'
else
  SN_SDK_EXEC_PREFIX='${exec_prefix}/sdk'
fi
AC_SUBST(SN_SDK_EXEC_PREFIX)

if test x$ac_win_build = xyes; then
   SN_PLATFORM_DIR=win
else
   SN_PLATFORM_DIR=unix
fi
AC_SUBST(SN_PLATFORM_DIR)

AC_SUBST(TK_DBGX)

AC_OUTPUT([Makefile
gui/Makefile
etc/navigato
hyper/Makefile
db/Makefile
packages/Makefile
parsers/Makefile
parsers/assembly/Makefile
parsers/chill/Makefile
parsers/cobol/Makefile
parsers/common/Makefile
parsers/cpp/Makefile
parsers/cpp/cpplib/Makefile
parsers/fortran/Makefile
parsers/gcj/Makefile
parsers/java/Makefile
parsers/libjcreuse/Makefile
parsers/m4/Makefile
parsers/python/Makefile
parsers/sendmail/Makefile
parsers/tcl/Makefile
parsers/toolbox/Makefile
bitmaps/Makefile
etc/Makefile
etc/english.txt
etc/sn_prop.cfg
etc/sn_toolchains/Makefile
snavigator/Makefile
misc/Makefile
misc/libutils/Makefile
sdk/Makefile
sdk/api/Makefile
sdk/api/tcl/Makefile
sdk/api/tcl/misc/Makefile
sdk/api/tcl/database/Makefile
sdk/api/tcl/database/examples/Makefile
sdk/api/c/Makefile
sdk/api/c/database/Makefile
sdk/api/c/database/examples/Makefile
sdk/parsers/Makefile
sdk/parsers/examples/Makefile
sdk/parsers/examples/assembly/Makefile
sdk/parsers/examples/elf/Makefile
snavigator/unix/Makefile
snavigator/unix/snavigator
snavigator/unix/snscenario
snavigator/win/Makefile
demo/Makefile
doc/Makefile
doc/html/Makefile])


